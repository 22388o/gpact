plugins {
    id 'java'
    id 'com.diffplug.spotless' version '5.12.5'
    id "com.github.node-gradle.node" version "3.0.1"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        mavenCentral()
    }

    spotless {
        java {
            target fileTree('.') {
                exclude '**/soliditywrappers/*.*'
                exclude '**/*.sol'
                exclude '**/.gradle/**'
                exclude '**/resources'
                exclude '**/build/**'
                include '**/*.java'
            }
            importOrder('net.consensys', 'java', '')
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            googleJavaFormat('1.12.0')
        }
    }
}

ext {
    log4JVersion = '2.14.1'
    okHttpVersion = '4.9.1'
    jacksonVersion = '2.13.0'
    rxJavaVersion = '2.2.21'
}

subprojects {
    test {
        useJUnitPlatform()
    }
    dependencies {
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4JVersion
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4JVersion
        implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okHttpVersion
        implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: okHttpVersion

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
        testCompileOnly 'junit:junit:4.13'
    }
}

task solFormat {
    dependsOn npm_run_solFormat
}

task solCheckFormat {
    dependsOn npm_run_solCheckFormat
}

task solhint {
    dependsOn npm_run_solhint
}

build.dependsOn solCheckFormat
build.dependsOn solhint